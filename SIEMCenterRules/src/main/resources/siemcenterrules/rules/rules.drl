package siemcenterrules.rules;

import java.util.List;
import java.time.LocalDateTime;
import com.securityoperationscenter.siemcenter.model.Account;
import com.securityoperationscenter.siemcenter.model.RiskLevel;
import com.securityoperationscenter.siemcenter.model.Alarm;
import com.securityoperationscenter.siemcenter.model.Log;
import com.securityoperationscenter.siemcenter.model.ErrorLog;
import com.securityoperationscenter.siemcenter.model.LoginLog;
import com.securityoperationscenter.siemcenter.model.PaymentLog;
import com.securityoperationscenter.siemcenter.model.AntivirusThreatDetectionLog;
import com.securityoperationscenter.siemcenter.SIEMCenterService;

global List maliciousIpAddresses;
global SIEMCenterService siemCenterService;

rule "Detect when ErrorLog appears"
    when
       ErrorLog()
    then
      System.out.println("RULE FIRED: Detect when ErrorLog appears.");
end

rule "Detect malicious ip address"
    when
        Log(machine.ip memberOf maliciousIpAddresses)
    then
        System.out.println("RULE FIRED: Detect malicious ip address.");
end

rule "Fire payment system alarm"
	when
        $log: PaymentLog($username: username, $timestamp: timestamp)
        not PaymentLog(username == $username , timestamp.isAfter($timestamp))

        Number(intValue >= 50) from accumulate(
            $paymentLogs: PaymentLog(
                username == $username,
                timestamp.isAfter(LocalDateTime.now().minusSeconds(60))
            ),
            count($paymentLogs)
        )
	then
		Alarm alarm = new Alarm("PAYMENT", "Payment system alarm!", $username, LocalDateTime.now());
        siemCenterService.saveAlarm(alarm); // comment for tests
        insert(alarm);
end

rule "Set account risk level to low when no alarm in past 90 days"
   no-loop
   when
       $account: Account($username: username)

       not Alarm(
           username == $username,
           timestamp.isAfter(LocalDateTime.now().minusDays(90))
       )
   then
       modify($account) {
           setRiskLevel(RiskLevel.LOW);
       }

       System.out.println("RULE FIRED: Set account risk level to low when no alarm in past 90 days for user: " + $username + ".");
end

rule "3 unsuccessful logins with the same username"
    when
        $log: LoginLog(successful != true, $username: username, $timestamp: timestamp)
        not LoginLog(successful != true, username == $username , timestamp.isAfter($timestamp))

        Number(intValue >= 3) from accumulate(
            $logs: LoginLog(
                successful != true,
                username == $username
            ),
            count($logs)
        )
    then
        Alarm alarm = new Alarm("THREE_UNSUCCESSFUL_LOGINS", "Three unsuccessful logins alarm!", $username, LocalDateTime.now());
        siemCenterService.saveAlarm(alarm); // comment for tests
        insert(alarm);
end

rule "7 antivirus threats from the same machine in the past 10 days."
    when
        threat: AntivirusThreatDetectionLog($machine: machine, $timestamp: timestamp )
        not AntivirusThreatDetectionLog(machine.ip == $machine.ip, timestamp.isAfter($timestamp))

        Number(intValue >= 7) from accumulate(
            $threats: AntivirusThreatDetectionLog(
                machine.ip == $machine.ip,
                timestamp.isAfter(LocalDateTime.now().minusDays(10))
            ),
            count($threats)
        )
    then
        Alarm alarm = new Alarm("SEVEN_ANTIVIRUS_THREATS", "Seven antivirus threats alarm!", null, LocalDateTime.now());
        siemCenterService.saveAlarm(alarm); // comment for tests
        insert(alarm);
end

rule "Login from account inactive for 90 days"
    when
        $log: LoginLog($username : username, successful == true)
        not LoginLog(username == $username, timestamp.isAfter($log.getTimestamp()))
        not LoginLog(username == $username, successful == true, timestamp.isAfter(LocalDateTime.now().minusDays(90)))
    then
        Alarm alarm = new Alarm("LOGIN_FROM_INACTIVE_ACCOUNT", "Login from inactive account alarm!", $username, LocalDateTime.now());
        siemCenterService.saveAlarm(alarm); // comment for tests
        insert(alarm);
end

rule "15 unsuccessful logins from the same ip in 5 days"
    when
        $log: LoginLog(
            successful != true,
            $ip: machine.ip,
            timestamp.isAfter(LocalDateTime.now().minusDays(5))
        )
        not LoginLog(
            successful != true,
            $ip == machine.ip,
            timestamp.isAfter(LocalDateTime.now().minusDays(5)),
            timestamp.isAfter($log.getTimestamp())
        )

        Number(intValue >= 15) from accumulate(
            $logs: LoginLog(
                successful != true,
                $ip == machine.ip,
                timestamp.isAfter(LocalDateTime.now().minusDays(5))
            ),
            count($logs)
        )
    then
        Alarm alarm = new Alarm("UNSUCCESSFUL_LOGINS_SAME_IP", "15 unsuccessful logins from the same IP alarm!", $ip, LocalDateTime.now());
        siemCenterService.saveAlarm(alarm); // comment for tests
        insert(alarm);
end